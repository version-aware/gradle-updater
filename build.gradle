buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'cz.augi:gradle-wartremover:0.9.6'
        classpath 'cz.alenkacz:gradle-scalafmt:1.7.2'
        classpath 'cz.augi:gradle-docker-java:1.7.0'
        classpath 'com.avast.gradle:gradle-docker-compose-plugin:0.9.1'
    }
}

plugins {
    id 'com.github.ben-manes.versions' version '0.21.0'
}

apply plugin: 'scala'
apply plugin: 'application'
apply plugin: 'scalafmt'
apply plugin: 'wartremover'
apply plugin: 'docker-java'
apply plugin: 'docker-compose'

group 'com.versionaware'
version = version == 'unspecified' ? 'DEVELOPER-SNAPSHOT' : version
mainClassName = 'com.versionaware.gradleupdater.Program'

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.scala-lang:scala-library:2.12.8'
    compile 'com.typesafe.scala-logging:scala-logging_2.12:3.9.2'
    compile 'ch.qos.logback:logback-classic:1.2.3'
    compile 'com.github.scopt:scopt_2.12:3.7.1'

    compile 'com.softwaremill.sttp:core_2.12:1.5.11'
    compile 'io.circe:circe-parser_2.12:0.11.1'
    compile 'io.circe:circe-generic_2.12:0.11.1'

    compile 'org.gitlab4j:gitlab4j-api:4.9.17'

    testCompile 'junit:junit:4.12'
    testCompile 'org.scalatest:scalatest_2.12:3.0.5'
}

sourceSets {
    main {
        resources {
            srcDir projectDir include 'gradle/wrapper/gradle-wrapper.jar', 'gradle/wrapper/gradle-wrapper.properties', 'gradlew', 'gradlew.bat'
        }
    }
}

dockerJava {
    image = "versionaware/gradle-updater:$version"
    alternativeImages = ['versionaware/gradle-updater:latest']
    username = System.getenv('DOCKER_USERNAME')
    password = System.getenv('DOCKER_PASSWORD')
}

dockerCompose.isRequiredBy(test)

wartremover {
    warningWarts.removeAll(['DefaultArguments', 'NonUnitStatements', 'Null', 'AsInstanceOf'])
}

test {
    testLogging {
        events 'failed'
        exceptionFormat 'full'
    }
}
